def isApp = rootProject.ext.isApplication
if (isApp) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

def androidV = rootProject.ext.android
def dependenciesV = rootProject.ext.dependencies

android {
    compileSdkVersion androidV.compileSdkVersion
    defaultConfig {

        if(isApp){
            applicationId "com.any.imagelibrary"
        }

        minSdkVersion androidV.minSdkVersion
        targetSdkVersion androidV.targetSdkVersion
        versionCode androidV.versionCode
        versionName androidV.versionName
        resourcePrefix "imgpk"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }


    sourceSets {
        main {
            if (isApp) {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    exclude 'debug/**'
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation dependenciesV.supportV7
    implementation dependenciesV.recyclerview
    implementation dependenciesV.fileProvider
    implementation dependenciesV.luBan


    testImplementation dependenciesV.junit
    androidTestImplementation dependenciesV.testRunner
    androidTestImplementation dependenciesV.espressoCore

    implementation project(path : ':baselibrary')
}
